buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.28.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ru.fitsme.android"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 24
        versionName "2.5.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "FitsMe-v${versionName}-debug.apk"
                }
            }
        }
    }
    buildToolsVersion '28.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    String archLifecycleVersion = '1.1.1'
    String glideVersion = '4.9.0'
    String retrofitVersion = '2.3.0'
    String okhttpVersion = '3.12.1'
    String stephanenicolasVersion = '1.1.3'
    String supportVersion = '28.0.0'

    // Support
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"

    // Android Arch
    implementation "android.arch.lifecycle:extensions:$archLifecycleVersion"
    implementation "android.arch.lifecycle:reactivestreams:$archLifecycleVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archLifecycleVersion"

    // Navigation
    implementation 'ru.terrakok.cicerone:cicerone:3.0.0'

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Dependency Injection
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:$stephanenicolasVersion"
    implementation "com.github.stephanenicolas.toothpick:smoothie:$stephanenicolasVersion"
    annotationProcessor "com.github.stephanenicolas.toothpick:toothpick-compiler:$stephanenicolasVersion"

    implementation 'commons-codec:commons-codec:1.10'

    // Glide
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"

    // Paginated List
    implementation 'android.arch.paging:runtime:1.0.1'
    implementation 'android.arch.paging:rxjava2:1.0.1'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
        transitive = true
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation "com.github.stephanenicolas.toothpick:toothpick-testing:$stephanenicolasVersion"

    // Masked text
    implementation 'com.redmadrobot:inputmask:4.0.0'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.31'
}
